apply plugin: 'com.android.application'

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: 'tasks.gradle'


android {
    compileSdkVersion 30

    defaultConfig {
        versionCode 45
        versionName '3.1'
        applicationId 'com.dg.eqs'

        minSdkVersion 21
        targetSdkVersion 30

        resConfigs 'en', 'de'

        testInstrumentationRunner 'com.dg.eqs.EqsAndroidJUnitRunner'

        kapt {
            arguments {
                arg 'room.schemaLocation', 'app/db/schemas'
            }
        }
    }

    sourceSets {
        main {
            java {
                srcDirs += 'src/main/kotlin'
            }
        }

        test {
            java {
                srcDirs += ['src/test/kotlin', 'src/testShared/kotlin']
            }
        }

        androidTest {
            java {
                srcDirs += ['src/androidTest/kotlin', 'src/testShared/kotlin']
            }

            assets {
                srcDirs += 'db/schemas'
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = false
        }

        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs += '-Xopt-in=kotlin.ExperimentalStdlibApi'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        animationsDisabled true
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        pickFirst 'META-INF/licenses/ASM'
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    // ---------------------------------------------------
    // PLATFORM DEPENDENCIES
    // ---------------------------------------------------
    kapt 'androidx.room:room-compiler:2.3.0'
    kapt 'androidx.lifecycle:lifecycle-common-java8:2.3.1'

    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.room:room-ktx:2.3.0'
    implementation 'androidx.room:room-runtime:2.3.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.activity:activity-ktx:1.2.2'

    // Billing flow does not complete in newest release
    implementation 'com.android.billingclient:billing:3.0.0'

    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // GameMark not shown in newest release after scrolling first
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'com.google.firebase:firebase-database-ktx:19.7.0'
    implementation 'com.google.android.gms:play-services-auth:19.0.0'
    implementation 'com.google.android.gms:play-services-games:21.0.0'
    implementation 'com.google.firebase:firebase-analytics-ktx:18.0.3'
    implementation 'com.google.firebase:firebase-crashlytics-ktx:17.4.1'

    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // ---------------------------------------------------
    // EXTERNAL DEPENDENCIES
    // ---------------------------------------------------
    kapt 'com.google.dagger:dagger-compiler:2.35.1'

    implementation 'com.google.dagger:dagger:2.35.1'
    implementation 'com.google.guava:guava:30.1-jre'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'org.mockito:mockito-inline:3.8.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:3.2.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.3'

    androidTestImplementation 'org.assertj:assertj-core:3.19.0'
    androidTestImplementation 'org.mockito:mockito-android:3.8.0'
    androidTestImplementation 'com.github.tmurakami:dexopener:2.0.5'
    androidTestImplementation 'org.mockito.kotlin:mockito-kotlin:3.2.0'
    androidTestImplementation 'com.github.fabioCollini.daggermock:daggermock:0.8.5'
    androidTestImplementation 'com.github.fabioCollini.daggermock:daggermock-kotlin:0.8.5'

    // Check for duplicated dependencies with: gradlew app:dependencies > deps.txt
    // Check for updated dependencies with: gradlew dependencyUpdates -Drevision=release
}